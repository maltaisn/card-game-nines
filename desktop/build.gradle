plugins {
    id "kotlin"
}

def assetsDir = file("../assets")
def mainClassName = "com.maltaisn.nines.desktop.DesktopLauncher"

sourceSets {
    main {
        java.srcDirs = ["src/main/kotlin"]
    }
}

dependencies {
    implementation project(":core")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_6
    targetCompatibility = JavaVersion.VERSION_1_6
}

task run(dependsOn: classes, type: JavaExec) {
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = assetsDir
    ignoreExitValue = true
}

task dist(type: Jar) {
    from files(sourceSets.main.output.classesDirs)
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    from assetsDir

    manifest {
        attributes "Main-Class": mainClassName
    }
}